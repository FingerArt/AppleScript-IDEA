{

  parserClass="com.idea.plugin.applescript.lang.parcer.AppleScriptParser"
  psiImplUtilClass="com.idea.plugin.applescript.psi.impl.AppleScriptPsiImplUtil"

  parserUtilClass="com.idea.plugin.applescript.lang.parser.AppleScriptGeneratedParserUtil" //for external grammar

    extends="com.idea.plugin.applescript.psi.impl.AppleScriptPsiElementImpl"
    implements="com.idea.plugin.applescript.psi.AppleScriptPsiElement"

    psiClassPrefix="AppleScript"
    psiImplClassSuffix="Impl"
    psiPackage="com.idea.plugin.applescript.psi"
    psiImplPackage="com.idea.plugin.applescript.psi.impl"

    elementTypeHolderClass="com.idea.plugin.applescript.psi.AppleScriptTypes"
    elementTypeClass="com.idea.plugin.applescript.lang.parser.AppleScriptElementType"
    tokenTypeClass="com.idea.plugin.applescript.lang.lexer.AppleScriptTokenType"
    tokens = [

  // NewLines and spaces //
  //  NLS ='regexp:{ONE_NL}({ONE_NL}|{WHITE_SPACE})*'
    NLS ='regexp:(\r|\n|\r\n)( (\r|\n|\r\n)|([\ \t\f]) )*'
    mONE_NL = 'regexp:(\r|\n|\r\n)'                                   // NewLines

    LPAREN='('
    RPAREN=')'

  // containment
  //list,string
    STARTS_BEGINS_WITH='regexp:(starts? with)|(begins? with)'
    ENDS_WITH='regexp:ends? with'
    //list,record,string
    DOES_NOT_CONTAIN="regexp:(does not contain)|(doesn't contain)"
    IS_IN="regexp:(is in)|(is contained by)"
    IS_NOT_IN="regexp:(is not in)|(is not contained by)|(isn't contained by)"
    IS_CONTAIN="regexp:(contain|contains)"

    // comparison operators
    GT="regexp:(>|((is)? greater than)|(comes after)|(is not less than or equal (to)?)|(isn't less than or equal (to)?))"
    LE="regexp:(≤|<=|((is)? less than or equal (to)?)|(is not greater than)|(isn't greater than)|(does not come after)|(doesn't come after))"
    GE="regexp:(≥|>=|((is)? greater than or equal (to)?)|(is not less than)|(isn't less than)|(does not come before)|(doesn't come before))"
    LT="regexp:(<|((is)? less than)|(comes before)|(is not greater than or equal (to)?)|(isn't greater than or equal (to)?))"
    NE="regexp:((isn't equal (to)?)|(is not equal (to)?)|(doesn't equal)|(does not equal)|(is not)|(isn't))"
    EQ="regexp:(=|((is)? equal to)|is|equal|equals)"
    REF_OP="regexp:(a reference to|a ref to|a ref|reference to|ref)"
//    GT='regexp:(">"|"is greater than"|"greater than"|"comes after"|"is not less than or equal to"|"is not less than or equal"|"isn't less than or equal to"|"isn't less than or equal")'
//    LE='regexp:("?"|"<="|"is less than or equal to"|"less than or equal to"|"is less than or equal"|"less than or equal"|"is not greater than"|"isn't greater than"|"does not come after"|"doesn't come after")'
//    GE='regexp:("?"|">="|"is greater than or equal to"|"greater than or equal to"|"is greater than or equal"|"greater than or equal"|"is not less than"|"isn't less than"|"does not come before"|"doesn't come before")'
//    LT='regexp:("<"|"is less than"|"less than"|"comes before"|"is not greater than or equal to"|"is not greater than or equal"|"isn't greater than or equal to"|"isn't greater than or equal")'
//    NE='regexp:("isn't equal to"|"isn't equal"|"is not equal to"|"is not equal"|"doesn't equal"|"does not equal"|"is not"|"isn't")'
//    EQ='regexp:("="|"is equal to"|"equal to"|"is"|"equal"|"equals")'
//    REF_OP='regexp:("a reference to"|"a ref to"|"a ref"|"reference to"|"ref")'

    CC='¬'
    PLUS='+'
    MINUS='-'
    STAR='*'
    DIV='regexp:(/|÷)'
    INT_DIV='div'
    MOD='mod'
    POW='^'
    LOR='or'
    LAND='and'
    BAND='&'
    LNOT='not'
    AS='as'
    APS="'s"

    //built in types
    //==> builtInClassIdCommon ==>
    STRING='string'
    CLASS='class'
    CONSTANT='constant'
    LIST='list'
    DATA='data'
    REFERENCE='reference'
    STYLED_TEXT='regexp:styled[\ \t\f]+text'
    TEXT_ITEM='regexp:text[\ \t\f]+item'
    ITEM='item'
    FILE_SPECIFICATION='regexp:file[\ \t\f]+specification'
    INTERNATIONAL_TEXT='regexp:international[\ \t\f]+text'
    RGB_COLOR='regexp:rgb[\ \t\f]+color'
    STYLED_CLIPBOARD_TEXT='regexp:styled[\ \t\f]+clipboard[\ \t\f]+text'
    //==> builtInClassIdNative ==>
    ANY='any'
    BOOLEAN='boolean'
    DATE='date'
    FILE='file'
    ALIAS='alias'
    INTEGER='integer'
    LOCATION_SPECIFIER='regexp:location[\ \t\f]+specifier'
    NUMBER='number'
    POINT='point'
    REAL='real'
    RECORD='record'
    RECTANGLE='rectangle'
    SPECIFIER='specifier'
    TEXT='text'
    TYPE='type'
    //==> UNIT_TYPE_VALUE_CLASSES ==>
    GALLONS='gallons'
    LITRES='litres'
    LITERS='liters'
    QUARTS='quarts'

    GRAMS='grams'
    KILOGRAMS='kilograms'
    OUNCES='ounces'
    POUNDS='pounds'

    TEMPERATURE='regexp:degrees[\ \t\f]+(Celsius|Fahrenheit|Kelvin)'
    CUBIC_VOL='regexp:cubic[\ \t\f]+(centimetres|centimeters|feet|inches|metres|meters|yards)'
    SQUARE_AREA='regexp:square[\ \t\f]+(feet|kilometres|kilometers|metres|meters|miles|yards)'

    CENTIMETRES='centimetres'
    CENTIMETERS='centimeters'
    FEET='feet'
    INCHES='inches'
    KILOMETRES='kilometres'
    KILOMETERS='kilometers'
    METRES='metres'
    METERS='meters'
    MILES='miles'
    YARDS='yards'

    //todo not only for literals
    //==>built in properties==>
    BUILT_IN_PROPERTY='regexp:("class"|day|weekday|month|year|time|date string|time string|(quoted[\ \t\f]+form)|length|rest|reverse|contents)'
    //properties of standard classes
    //string
    QUOTED_FORM='regexp:quoted[\ \t\f]+form'

    //some keywords here which do not appear in rules distinctively
    THE_KW='the'
    DOES='does'
    APART_FROM='regexp:apart[\ \t\f]+from'
    ASIDE_FROM='regexp:aside[\ \t\f]+from'
    OUT_OF='regexp:out[\ \t\f]+of'
    INSTEAD_OF='regexp:instead[\ \t\f]+of'
    THAT='that'
    REFERENCE='reference'
//    ITS='its'
    C_WHITE_SPACE='regexp:(white[\ \t\f]+space)'
    APP_RESPONSES='regexp:((application[\ \t\f]+responses)|(app[\ \t\f]+responses))'
    SCRIPTING_ADDITIONS='regexp:scripting[\ \t\f]+additions'
    CURRENT_DATE='regexp:current[\ \t\f]+date'
    CURRENT_APPLICATION='regexp:current[\ \t\f]+application'
    CURRENT_APP='regexp:current[\ \t\f]+app'
    CURRENT='current'
    APPLICATION='application'
    TEXT_ITEM_DELIMETERS='regexp:text[\ \t\f]+item[\ \t\f]+delimiters'
    MISSING_VALUE='regexp:missing[\ \t\f]+value'

    //constants
    PI_CONSTANT='pi'              //3.1412926
    MINUTES_CONSTANT='minutes'    // =60seconds
    HOURS_CONSTANT='hours'        //60*60
    DAYS_CONSTANT='days'          //60*60*24
    WEEKS_CONSTANT='weeks'

    STRING_LITERAL='regexp:"([^"\\]|\\.)*"'
    DIGITS='regexp:[0-9]+'

    DEC_EXPONENT='regexp:[Ee][+-]?[0-9]+'
    var_identifier='regexp:([_a-zA-Z][a-zA-Z0-9_]*)|(\|(([^\|])|(\\\|))*[^\\]\|)'
  //todo nested comments (in a lexer with the states...)
    COMMENT='regexp:((#.*)|(--.*)|((\(\*[^\*](([^\*]*(\*+[^\*\)])?)*(\*+\))?))|\(\*))'
    RAW_CODE='regexp:(<<[^>>]*>>)'
    MULTI_LINE_COMMENT_START='(*'
    MULTI_LINE_COMMENT_END='*)'
    WHITE_SPACE='regexp:[\ \t\f]'
    COMMA=','
    LCURLY='{'
    RCURLY='}'
    COLON=':'


  ]
    extends(".*Expression")="expression"
}

appleScriptFile ::= compilation_unit_

private compilation_unit_ ::=
                      (
                        (statement sep*)
                      | (expression sep*)//of startup disk -> expects new line after startup without *
                      | (COMMENT sep*) //how to include space as a separator?
                      | (RAW_CODE sep*)
                      | sep//|spaceSep
                      )*
                      //{recoverWhile="mainScriptRecover"}
//mainScriptRecover ::= !(with|tell|repeat|sep)
                      //{recoverWhile = not_next_expression}

private sep ::= NLS|COMMENT //one or more new lines and spaces
private nls ::= NLS* // zero or more new lines and spaces

// //todo can not be used for naming handlers
identifier ::= var_identifier//|appleScriptProperty

//appleScriptNativeHandler ::= run|quit|idle|open
//private identifierOrStandardCommands ::= identifier|applicationStandardCommandNames
referenceIdentifier ::= identifier //identifierOrStandardCommands
{mixin="com.idea.plugin.applescript.psi.impl.AppleScriptReferenceElementImpl"
implements="com.idea.plugin.applescript.psi.AppleScriptReferenceElement"}

private statement ::= controlStatement|propertyDeclarationStatement|varDeclarationList
                      |handlerDefinition
                      |scriptObjectDefinition
                      |scriptObjectUnnamedDefinition
                      |returnStatement|continue_statement
                      //|assignmentStatement
                      |parseAssignmentStatement


private handlerDefinition ::= handlerLabeledParametersDefinition|handlerPositionalParametersDefinition
//                      |handlerRunDefinition|handlerOpenDefinition|handlerIdleDefinition|handlerQuitDefinition
                      |handlerInterleavedParametersDefinition
returnStatement ::= return [expression]

private controlStatement ::= ifStatement | tryStatement | tell_statement | repeatStatement | exitStatement
|consideringStatement|ignoringStatement|withTimeoutStatement|withTransactionStatement|useStatement

exitStatement ::= exit [repeat]
private repeatStatement ::= repeatNumTimesStatement | repeatWhileStatement | repeatUntilStatement
| repeatWithRangeStatement | repeatWithListStatement | repeatForeverStatement

blockBody ::= (statement|COMMENT|expression|RAW_CODE)? (nls (statement|COMMENT|expression|RAW_CODE))* {pin=1}

repeatForeverStatement ::= repeat nls
                            blockBody? nls
                           end [repeat]

repeatNumTimesStatement ::= repeat numTimes [times] nls
                            blockBody? nls
                            end [repeat]

private numTimes ::= integerLiteralExpression|referenceIdentifier|parenthesizedExpression

repeatWhileStatement ::= repeat while expression nls
                            blockBody? nls
                         end [repeat] {pin=2}

repeatUntilStatement ::= repeat until expression nls
                         blockBody? nls
                         end [repeat] {pin=2}

repeatWithRangeStatement ::= repeat with loopVariable from startValue to stopValue [by stepValue] nls
                             blockBody? nls
                             end [repeat] {pin=4}

//todo may be just use expression here
private stepValue ::= integerLiteralExpression|referenceIdentifier|parenthesizedExpression|prefixExpression
private stopValue ::= integerLiteralExpression|referenceIdentifier|parenthesizedExpression|prefixExpression
private startValue ::= integerLiteralExpression|referenceIdentifier|parenthesizedExpression|prefixExpression
private loopVariable ::= directParameterDeclaration


repeatWithListStatement ::= repeat with loopVariable in listOrReferenceExpression nls
                         blockBody? nls
                         end [repeat] {pin=4}
//todo test all possible list expressions (may be it is better to specify any expansion here)
private listOrReferenceExpression ::= listLiteralExpression|objectRefWrapper

errorCommandExpression ::= error [errorMessage] [number errorNumber]
                                 [from offendingObject]
                                 [to expectedType]
                                 [partial result resultList] //{pin=1}
private resultList ::= listLiteralExpression|referenceIdentifier
//offendingObject is a reference to the object, if any, that caused the error. If you provide a partial reference,
// AppleScript completes it using the value of the default object (for example, the target of a Tell statement).
private offendingObject ::= typeSpecifierD|objectRefWrapper

tryStatement ::= try nls
                     blockBody? nls
                     on error [errorMessageVar] [number errorNumberVar] [from offendingObjectVar]
                     [to expectedTypeVar] [partial result resultListVar] nls
                     [varDeclarationList] nls
                     blockBody? nls
                 end [error|try] {pin=1}

varDeclarationListPart ::= identifier
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
 implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

varDeclarationList ::= varAccessDeclaration (COMMA varDeclarationListPart)*

private errorMessageVar ::= directParameterDeclaration
private errorNumberVar ::= directParameterDeclaration
private offendingObjectVar ::= referenceIdentifier
private resultListVar ::= referenceIdentifier
private expectedTypeVar ::= referenceIdentifier

//todo: Using it, me, and my in Tell Statements ! p.242
private tell_statement ::= tell_simple_statement|tell_compound_statement
// it - ref to object, me - ref to current script

private applicationClass ::= APPLICATION|app

// tell front window of application "Finder" to close | object_specifier_statement == front window of application "Finder"
// For more information on how to specify an application object, see the application class.

//private tellObjectReference ::= objectRefWrapper
//object_specifier_statement is a reference to an application object, system object, or script object.
tell_compound_statement ::= tell objectRefWrapper nls //todo not to count Ø -| and CRLF char as a sep
                                        blockBody? nls
                            end [tell] {pin=6}

private expectedType ::= typeSpecifierD|referenceIdentifier
private errorNumber ::= integerLiteralExpression|referenceIdentifier
private errorMessage ::= stringLiteralExpression|referenceIdentifier

consideringStatement ::= considering considerOrIgnoreAttribute ((COMMA considerOrIgnoreAttribute)* (LAND considerOrIgnoreAttribute)?)? nls
                           [but ignoring considerOrIgnoreAttribute ((COMMA considerOrIgnoreAttribute)* (LAND considerOrIgnoreAttribute)?)? nls]
                           blockBody? nls
                         end considering {pin=2}

ignoringStatement ::= ignoring considerOrIgnoreAttribute ((COMMA considerOrIgnoreAttribute)* (LAND considerOrIgnoreAttribute)?)? nls
                        [but considering considerOrIgnoreAttribute ((COMMA considerOrIgnoreAttribute)* (LAND considerOrIgnoreAttribute)?)? nls]
                           blockBody? nls
                      end ignoring {pin=2}
considerOrIgnoreAttribute ::= expression //(APP_RESPONSES)|case|diacriticals|expansion|hyphens|punctuation|C_WHITE_SPACE
//considerOrIgnoreAttribute ::= (APP_RESPONSES)|case|diacriticals|expansion|hyphens|punctuation|C_WHITE_SPACE

withTimeoutStatement ::= with timeout [of] integerLiteralExpression (seconds|second !(classTypeName)) nls //index ref
                           blockBody? nls
                         end [timeout] {pin=2}

withTransactionStatement ::= with transaction [sessionSpecificator] nls
                               blockBody? nls
                             end [transaction] {pin=2}
private sessionSpecificator ::= referenceIdentifier

//todo may define just single 'use' statement?
useStatement ::= useAppleScriptStatement|useScriptingAdditionsStatement|useApplicationOrScriptStatement
|useFrameworkStatement|usingTermsFromStatement
//todo 'considering numeric strings'

private useAppleScriptStatement ::= use 'AppleScript' [version stringLiteralExpression] {pin=2}

private useScriptingAdditionsStatement ::= use SCRIPTING_ADDITIONS
                                   [((with importing)|(without importing)|importing) expression ] {pin=2}

private useApplicationOrScriptStatement ::= use [referenceIdentifier COLON] (script|applicationClass) dataSpecifier
                                       [version stringLiteralExpression ]
                                       [((with importing)|(without importing)|importing) expression ] {pin=3}
private dataSpecifier ::= referenceExpression|stringLiteralExpression

private useFrameworkStatement ::= use framework dataSpecifier {pin=2}

private usingTermsFromStatement ::= using terms from (application_reference|(script dataSpecifier)|(SCRIPTING_ADDITIONS)) nls
                               blockBody? nls
                            end [using terms from] {pin=3}

//todo to check com.jetbrains.python.psi.PyAssignmentStatement#getTargetsToValuesMapping
//todo and com.jetbrains.python.psi.impl.PyTargetExpressionImpl#findAssignedValue
//todo see also com.jetbrains.python.psi.impl.PyAssignmentStatementImpl#mapToValues
targetVariable ::= identifier
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

private ifStatement ::= ifCompoundStatement|ifSimpleStatement

ifSimpleStatement ::= if expression then (/*!(ifCompoundStatement)*/ (statement|expression) ) //todo only simple statements are allowed here
//                                    {pin=4}

ifCompoundStatement ::= if expression [then] sep //sep is important - otherwise if simple statement is parsed
                                       blockBody? nls
                                   (else if expression [then] nls
                                       blockBody? nls )*
                                   (else nls
                                        blockBody? nls)?
                                   end [if] {pin=10}

// value class types
 literalExpression ::=
              stringLiteralExpression
              |styledTextLiteralExpression
              //|built_in_type
              |numberLiteralExpression
              |constantLiteralExpression
              |listLiteralExpression
              |recordLiteralExpression
              |dateLiteralExpression
              |aReferenceToLiteralExpression

/** Numeric, string, regexp, boolean, date, string(space,tab,return) constant*/
//todo: is also componentName ??? for ex. parent - property can hold a value inside, but true/false can not -
//todo is more like language keyword
constantLiteralExpression ::= boolean_constant|date_time_constants|save_opt_constant|text_constant|itMeProperty
|current_application_constant| dictionaryConstant | missing_value_constant
//|appleScriptPropertiesConstant

dictionaryConstant ::= <<parseDictionaryConstant>>

// SIMPLE literals

// Number class<<
 numberLiteralExpression ::= realLiteralExpression|integerLiteralExpression|numericConstant

// >>Integer class<<
integerLiteralExpression ::= DIGITS

// A value of class Class is a class identifier. A class identifier is a reserved word that specifies the class
// to which an object or value belongs. The Class property of an object contains a class identifier value.
// >>Class class<<
builtInClassIdentifier ::= builtInClassIdCommon|builtInClassIdNative|script

//there is no need to define in SDEF these classes:
private builtInClassIdNative::= ANY|BOOLEAN|DATE|FILE|INTEGER|LOCATION_SPECIFIER|NUMBER|POINT|REAL|RECORD|RECTANGLE|
SPECIFIER|TEXT|TYPE     |ALIAS

private builtInClassIdCommon::= STRING|CLASS|CONSTANT|LIST|DATA|REFERENCE|STYLED_TEXT|TEXT_ITEM|ITEM|FILE_SPECIFICATION|
INTERNATIONAL_TEXT|RGB_COLOR|STYLED_CLIPBOARD_TEXT|unitTypeValueClasses

private unitTypeValueClasses ::= Length|SQUARE_AREA|CUBIC_VOL|LiquidVolume|Weight|TEMPERATURE
private Weight ::= GRAMS|KILOGRAMS|OUNCES|POUNDS
private LiquidVolume ::= GALLONS|LITRES|LITERS|QUARTS
private Length ::= CENTIMETRES|CENTIMETERS|FEET|INCHES|KILOMETRES|KILOMETERS|METRES|METERS|MILES|YARDS

//todo these classes are not found in standard additions:
//but declared as properties...
builtInClassIdentifierPlural ::= builtInClassIdentifier"s"|scripts

//>>Real class<<
realLiteralExpression ::= ('.'(DIGITS)(DEC_EXPONENT))|(dec_significand DEC_EXPONENT?)
private dec_significand ::= ('.'DIGITS)|(DIGITS'.'DIGITS*)

// >>Date class<<
dateLiteralExpression ::= (date stringLiteralExpression)|currentDateCommand // hack! scripting addition command Current Date !
private currentDateCommand ::= CURRENT_DATE

//>> List reference
//private list_specifier ::= (class|length|rest|reverse) of list_literal_expression // ref by properties
//                            item [index]
//A value of class Constant is a reserved word defined by AppleScript or an application in its dictionary.
// Applications define sets of values that can be used for parameters of a particular command. For example,
// the value of the saving parameter of a Close command must be one of the three constants yes, no, and ask
//For a complete listing of the constants AppleScript provides, see Table A-3(page 358) and Table A-2 (page 355).
// !! NOT CASE SENSITIVE !!
//You cannot define your own constants; constants can be defined only by applications and AppleScript.
// >>Constant class<<
//private constant_literal_expression ::= boolean_constant|date_time_constants//number_constant to number_literal
//                            |missing_value_constant|result_constant|anything_constant|save_opt_constant
//                            |string_constant|version_constant

textItemDelimitersProperty ::= TEXT_ITEM_DELIMETERS

//>>Record class<<
recordLiteralExpression ::= LCURLY[objectPropertyDeclaration] (COMMA objectPropertyDeclaration)* RCURLY//(BAND nls primaryExpression)*

targetRecordLiteral ::= LCURLY[objectTargetPropertyDeclaration] (COMMA objectTargetPropertyDeclaration)* RCURLY
{ methods=[getTargets] }
//todo inspection for only unique property label names

//>>String class<<
stringLiteralExpression ::= STRING_LITERAL //(BAND primaryExpression)* //sep results in more that one new line

//>>List class<<
listLiteralExpression ::= LCURLY [expression] (COMMA expression)* RCURLY

targetListLiteral ::= LCURLY [targetVariablePattern|expression] (COMMA targetVariablePattern|expression)* RCURLY //{pin=4}
{methods = [getTargets] }

//The class identifier Styled Text is a synonym for a string that includes style and font information.
//>>Styled Text class<<
private styledTextLiteralExpression ::= STRING_LITERAL

//You can use the class identifier Text as a synonym for the identifier String
//>>Text class<<
//private text_literal ::= string_literal

// >>Boolean class<<
private boolean_constant ::= true|false

// COMMANDS
// commands  is a word or a series of words used in AppleScript statements to request an action.
// we need to parse xml files -> index command names and signatures they have -> after user inters some command

//An AppleScript command is one that is built into the AppleScript language. There currently are five such
// commands: get , set, count, copy, and run.
// Except for copy , each of these commands can also be implemented by applications.
//todo using the keyword anywhere in references (other cases perhaps?)

application_reference ::= THE_KW? applicationClass STRING_LITERAL [of THE_KW? machine STRING_LITERAL (of THE_KW? zone STRING_LITERAL)?]
                                |THE_KW? current_application_constant

file_reference ::= THE_KW?(FILE|ALIAS) expression // example p.193

left applicationObjectReference ::= (of|in) THE_KW? application_reference

//todo any handler can only be attached to the identifier!
private refForm ::= (my handlerInterleavedCallOrPropertyReference )
                    |(my?
                    (arbitraryReference
                    |everyElemReference
                    |rangeReference
                    |nameReference
                    |middleElemReference
                    |relativeReference
                    |indexReference
                    |propertyReference //handlerCallExpression?
                     ))

private handlerInterleavedCallOrPropertyReference ::= handlerInterleavedParametersCall | propertyReference
//expr like: my point()
private handlerInterleavedCallOrPropertyReferenceDOrReferenceIdentifier ::= handlerInterleavedParametersCall | ((propertyReferenceD|referenceIdentifier) handlerPositionalParametersCall) | referenceIdentifier

propertyReference ::=  propertyLabelReference

propertyReferenceD ::=  dictionaryPropertyName|literalExpression|appleScriptProperty|builtInClassProperty//typeSpecifierD|referenceIdentifier//referenceIdentifier goes lower
//private propertyLabelIdentifier ::= dictionaryPropertyName|literalExpression|appleScriptProperty|builtInClassProperty|
//identifier
//todo better define or not define it here?
//|appleScriptProperty

//todo why builtInClassNamePlural is valid here?'text items': 'set len to (length of (text items of item))'
private propertyLabelReference ::= dictionaryPropertyName|literalExpression|appleScriptProperty|
builtInClassProperty|referenceIdentifier //|builtInClassIdentifier|builtInClassIdentifierPlural




dictionaryPropertyName ::= <<parseDictionaryPropertyName>>
private dictionaryPropertyNameTokens ::= identifier|builtInClassIdentifier|builtInClassIdentifierPlural|handlerParameterLabel|script

private builtInClassProperty ::= stringProperty | BUILT_IN_PROPERTY

private stringProperty ::= QUOTED_FORM

//todo and how to resolve these names? - multiRangeReference?
//example: system preferences
private propertyLabelIdentifier ::= dictionaryPropertyName|literalExpression|appleScriptProperty|builtInClassProperty|
identifier
private scriptPropertyLabelIdentifier ::= propertyLabelIdentifier //builtInClassIdentifier|identifier//todo fix all such declared identifiers
private variableLabelIdentifier ::= identifier


arbitraryReference ::= some classTypeName {pin=1}
arbitraryReferenceD ::= some typeSpecifierD {pin=1}

//Plural forms are listed in application dictionaries.
everyElemReference ::= every (classTypeName|pluralClassName) {pin=1}
everyElemReferenceD ::= every typeSpecifierD {pin=1}
//words of myString

//The Filter reference form specifies all objects in a container that match one or  more conditions specified
// in a Boolean expression.
left filterReference ::=  (whose|where) expression

//The ID reference form specifies an object by the value of its ID property
//private id_reference ::= class_name id id_value //todo the syntax is not clear

//The Index reference form specifies an object or a location by describing its position with respect
// to the beginning or end of a container. For related information, see “Relative” (page 185).
//todo otherwise handler call in a chain can be detected as reference_expression
//searchFiles for ff of minimumValue(2, 3) for "LeChateau"
//todo change to expression
private indexValueExpression ::= (referenceIdentifier|/*reservedWordsNotParameterLabel|*/integerLiteralExpression)
//to exclude detecting as index handlerWithDirectLabeledParametersCall and other handler calls
 !(handlerParameterLabel|with|without|given|LPAREN|COLON)

indexReference ::= ((classTypeNameStrict [index] (indexValueExpression|parenthesizedExpression))
                   //|(classType [index] indexValueExpression)
                   |(integerLiteralExpression('st'|'rd'|'th') classTypeName) //how to concatenate?
                   |((first|second|third|fourth|fifth|sixth|seventh|eighth|ninth|tenth) classTypeName)
                   |((last|front|back) classTypeName))

indexReferenceD ::= ((typeSpecifierD [index] (indexValueExpression|parenthesizedExpression))
                   |(integerLiteralExpression('st'|'rd'|'th') (typeSpecifierD|userClassName)) //how to concatenate?
                   |((first|second|third|fourth|fifth|sixth|seventh|eighth|ninth|tenth) (typeSpecifierD|userClassName) )
                   |((last|front|back) (typeSpecifierD|userClassName)))

//private typeSpecifier ::= builtInClassName|builtInClassNamePlural|applicationClassName|script|dictionaryClassName
//|userClassName

dictionaryClassName ::= <<parseDictionaryClassName>>

userClassName ::= identifier
{mixin="com.idea.plugin.applescript.psi.impl.AppleScriptReferenceElementImpl"
implements="com.idea.plugin.applescript.psi.AppleScriptReferenceElement"}

middleElemReference ::= middle classTypeName {pin=1}
middleElemReferenceD ::= middle typeSpecifierD {pin=1}
nameReference ::= classTypeName [named] nameString
nameReferenceD ::= typeSpecifierD [named] nameString
private nameString ::= stringLiteralExpression//|parenthesized_expression //(smth as string) or get the string from some val

rangeReference ::= (every classTypeName from boundaryReference1 to boundaryReference2)
                   |(pluralClassName from boundaryReference1 to boundaryReference2)
                   |((/*refForm |*/ classTypeName) startIndex (thru|through) stopIndex)
                   |(pluralClassName startIndex (thru|through) stopIndex)
                   
rangeReferenceD ::= (every typeSpecifierD from (beginning|expression) to (end|expression) )
                   |(pluralClassName from (beginning|expression) to (end|expression) )//todo what todo with plural classNames ?
                   |((/*refForm |*/ typeSpecifierD) startIndex (thru|through) stopIndex)
                   |(pluralClassName startIndex (thru|through) stopIndex)
private stopIndex::= integerLiteralExpression | expression
private startIndex ::= integerLiteralExpression | expression
private boundaryReference1 ::= beginning|expression//parenthesized_expression  here??
private boundaryReference2 ::= end|expression

relativeReference ::= ([classTypeName](before|([in] ( front | beginning ) of) )referenceExpression)
                       |([classTypeName](after|([in]( back | end ) of)|behind )referenceExpression)
                       
relativeReferenceD ::= ([typeSpecifierD](before|([in] ( front | beginning ) of) )referenceExpressionD)
                       |([typeSpecifierD](after|([in]( back | end ) of)|behind )referenceExpressionD)

// >>Reference class<<
//private aRefTo ::= 'a reference to'|'a ref to'|'a ref'|'reference to'|'ref'
aReferenceToLiteralExpression ::= REF_OP objectRefWrapper //referenceExpression
// <<//A value of class Reference is a reference to an object. A reference can refer to an application object
// such as a window or file, or to an AppleScript object such as an item in a list or a property in a record.
// You can create a value of class Reference by using the A Reference To operator. In addition, applications can
// return references in response to commands.
// !!! A value of class Reference created with the A Reference To operator is a structure within AppleScript
// that refers to (or points to) a specific object. !!! a pointer!
// Values of class Reference are similar to pointers in other programming languages, but unlike pointers,
// references can refer only to objects.

//===Coercion (Object Conversion)===
// Coercion (also known as object conversion) is the process of converting objects from one class to another.
// AppleScript converts an object to a different class in either of these circumstances:
//- in response to the as operator
//- automatically, when an object is of a different class than was expected for a particular command or operation

//private

private propertyDeclarationStatement ::= scriptPropertyDeclaration//|objectPropertyDeclaration//|propertyParentDeclarationStatement
//{mixin="com.idea.plugin.applescript.psi.impl.AbstractAppleScriptComponentplements="com.idea.plugin.applescript.psi.AppleScriptComponent"

varAccessDeclaration ::= variableGlobalDeclaration|variableLocalDeclaration
//todo method isGlobal()/isLocal() etc
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

//AppleScript propertyDeclaration
scriptPropertyDeclaration ::= (property|prop) scriptPropertyLabelIdentifier COLON propertyInitializerExpression
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

//Object (AppleScript record or application object) propertyDeclaration
//todo implement getIdentifier() method
objectPropertyDeclaration ::= propertyLabelIdentifier COLON propertyInitializerExpression
//{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
//implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

//fallback to expression - it is also valid construct
//todo identifier could consist from several identifiers !
objectTargetPropertyDeclaration ::= propertyLabelIdentifier COLON (targetVariablePattern|propertyInitializerExpression)
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

private variableGlobalDeclaration ::= global variableLabelIdentifier
private variableLocalDeclaration ::= local variableLabelIdentifier

private propertyInitializerExpression ::= expression
//An expression is any series of lexical elements that has a value. Expressions are used in scripts to represent
// or derive values. The simplest kinds of expressions, called literal expressions, are representations of values
// in scripts. More complex expressions typically combine literals, variables, operators, and object specifiers.
expression ::= logicalOrExpressionWrapper

parenthesizedExpression ::= LPAREN expressionInParentheses RPAREN

private expressionInParentheses ::= ( statement | expression )

// ===CONSTANTS===
// some of the constants (which are not expressions and are not evaluated at runtime should be
// written as lexer tokens
// constant expressions (like 'current application' constant whoch is an object specifier)
// probably should be written as predefined A.Script. expressions
// should we divide parsed constant expressions by types (boolean_constant|number_constant|
// text_constant|consider_ignore_attributes|... etc) and then insert them in expressions where appropriate?
// page 100
//pi|space|tab|linefeed|quote| //!return!
// all constant expressions we could specify in the among all other expressions => it will be valid

numericConstant ::= PI_CONSTANT|seconds_constants
private seconds_constants ::= MINUTES_CONSTANT|HOURS_CONSTANT|DAYS_CONSTANT|WEEKS_CONSTANT
private date_time_constants ::= weekday_constant|month_constant//seconds_constants to number_constants
private weekday_constant ::= 'Monday'|'Tuesday'|'Wednesday'|'Thursday'|'Friday'|'Saturday'
                            |'Sunday'|'Mon'|'Tue'|'Wed'|'Thu'|'Fri'|'Sat'|'Sun'
private month_constant ::= 'January'|'February'|'March'|'April'|'May'|'June'|'July'|'August'|'September'|'October'
                    |'November'|'December' |'Jan'|'Feb'|'Mar'|'Apr'|'Jun'|'Jul'|'Aug'|'Sep'|'Oct'|'Nov'|'Dec'

private current_application_constant ::= CURRENT_APPLICATION|CURRENT_APP|(CURRENT APPLICATION) //'current application'|'current app'//current applicationClass
private missing_value_constant ::= MISSING_VALUE
private save_opt_constant ::= yes|no|ask
private text_constant ::=  return|space|tab|linefeed|quote
private anythingProperty ::= anything
private resultProperty ::= result
private parentProperty ::= parent
private versionProperty ::= version

//todo redefine necessary methods (is property, getName etc.)
appleScriptProperty ::= BUILT_IN_PROPERTY|resultProperty|versionProperty/*|missing_value_constant*//*??*/|anythingProperty
|textItemDelimitersProperty|parentProperty//|itMeProperty//|text_constant /*?? */

private itMeProperty ::= it|me//|my//should not include my here strictly speaking

handlerParameterLabel ::=
about|above|against|'apart from'|around|'aside from'|at|below|beneath|beside|between   // spaces ??
                     |by|for|from|'instead of'|into|on|onto|'out of'|over|since|thru|through|under
|to //no such label in documentation but it works!

argumentSelector ::= identifier COLON
{methods=[getSelectorIdentifier getSelectorName]}

//private handlerNamePartReference ::= referenceIdentifier //|propertyLabelComponent
//An identifier for the direct parameter variable. If it is included, directParamName must be listed immediately
// after the command name. The word of or in before directParamName is required in user-defined handlers,
// but is optional in terminology-defined handlers (for example, those defined by applications).
private userParameterVar ::= THE_KW? targetVariablePattern /// instead of componentName
private userLabelReference ::= THE_KW? referenceIdentifier

//===HANDLERS===
// handler is a collection of statements which can be invoked by name

handlerLabeledParametersDefinition ::= (on|to) identifier !LPAREN labeledParameterDeclarationList
                                         [given THE_KW? objectTargetPropertyDeclaration (COMMA THE_KW? objectTargetPropertyDeclaration)*] //nls //should be sep here
                                         [varDeclarationList] nls
                                           blockBody? nls
                                        end [handlerNameStrict] //!expression// sep or EOF here todo think: sep is needed for validation but could be issues with not included handlerNameStrict
//                                        {pin=2}
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"
methods=[getParameterComponentList]}

labeledParameterDeclarationList ::= (of|in)? directParameterDeclaration? labeledParameterDeclarationPart*
{methods=[getComponentList]}
//directParameterDeclaration ::= directParameterVar
directParameterDeclaration ::= THE_KW? identifier
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

labeledParameterDeclarationPart ::= handlerParameterLabel THE_KW? identifier //userParameterVar //todo implement targetVariablePattern here !!!
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

private handlerNameStrict ::=(referenceIdentifier !handlerParameterLabel)*

//private userParameterVar2 ::= referenceIdentifier
//private userLabel2 ::=  referenceIdentifier

//todo may be include these keywords on the top level script only not for the all the expressions...??
//todo should we made it left associative with identifier only??
//handlerLabeledParametersCall ::= handlerWithDirectLabeledParametersCall|handlerNoDirectLabeledParametersCall
left handlerLabeledParametersCall ::=
   (
   (of|in)? directParameterVal
   (
     (    (handlerParameterLabel parameterVal)+
           ( (with labelForTrueParam (COMMA labelForTrueParam)* [(LAND|LOR|COMMA) labelForTrueParam])
           | (without labelForFalseParam (COMMA labelForFalseParam)* [(LAND|LOR|COMMA) labelForFalseParam])
           | (given userLabelReference COLON userParameterVal (COMMA userLabelReference COLON userParameterVal)*)
           )*
     )
     |
       ( (with labelForTrueParam (COMMA labelForTrueParam)* [(LAND|LOR|COMMA) labelForTrueParam])
       | (without labelForFalseParam (COMMA labelForFalseParam)* [(LAND|LOR|COMMA) labelForFalseParam])
       | (given userLabelReference COLON userParameterVal (COMMA userLabelReference COLON userParameterVal)*)
       )+
   )
   )|
   (
   (handlerParameterLabel parameterVal)+ //&(with|without|given)
       ( (with labelForTrueParam (COMMA labelForTrueParam)* [(LAND|LOR|COMMA) labelForTrueParam])
       | (without labelForFalseParam (COMMA labelForFalseParam)* [(LAND|LOR|COMMA) labelForFalseParam])
       | (given userLabelReference COLON userParameterVal (COMMA userLabelReference COLON userParameterVal)*)
       )*
   )
//{recoverWhile="until_to" pin(".*")=2}
//until_to ::= !(to|of)

//private subRoutineHandlerCall ::= handlerLabeledParametersCall
//1. set e to do shell script "column -t -s' ' <<< " & quoted form of e without altering line endings
//2. commandName .... 'with properties' {fff:"dsd"}  'with properties' is it parameter name - need to check in dictionary
//todo quoted form of e without altering line endings is a labeledHandler call !! check parsing order
//commandHandlerCallParameters ::= of? directParameterValD? (
//( &((with|without) commandParameterSelector)((with|without) commandParameterSelector)  ) //could be
//|(given? commandParameterSelector parameterValue) )*

//fo not delete -> tokens are used in generatedUtils
private parameterValue ::= expression
commandParameterSelector ::= <<parseCommandParameterSelector>>
commandParameter ::= given? commandParameterSelector parameterValue
//private commandParameters ::= ( ((with|without) commandParameterSelector) | commandParameter )*
//commandParameterSelector ::= <<parseCommandParameterSelector>>
//todo the same as for commands?
private dictionaryCommandParameterSelectorTokens ::= identifier|builtInClassIdentifier|builtInClassIdentifierPlural|handlerParameterLabel|with|without

//private dictionaryCommandNameTokens ::= identifier|builtInClassName|builtInClassNamePlural|handlerParameterLabel|run|count|script//|with|without|given set/copy ??

private labelForTrueParam ::= expression //referenceIdentifier
private labelForFalseParam ::= expression //referenceIdentifier

 parameterVal ::=
 handlerCallDirectParameterWithGuardPredicate|
 expression
private handlerCallDirectParameterWithGuardPredicate ::=
//sayHello to name ->
(&(referenceIdentifier handlerParameterLabel) referenceIdentifier)
//|(&(referenceExpression APS referenceExpression handlerParameterLabel)referenceExpression APS referenceExpression)
//set (AppleScripts text_item_delimiters) to {", "} ->
//|(&(parenthesized_expression handlerParameterLabel)parenthesized_expression)
private userParameterVal ::= expression

//todo need to add custom parsing
private directParameterValD ::= directParameterVal

 //todo to think why otherwise 'copy sayHello to name' is not detected as handler call (but only 'sayHello to name')
 //todo may be this is the correct behaviour??
//todo replace to just expression
 directParameterVal ::=
  handlerCallDirectParameterWithGuardPredicate|
  expression
  //{recoverWhile="setRecover"}
  //expression //todo if parameter=handlerLabeledParametersCall? (ident.+label)

tell_simple_statement ::= tell 
(handlerCallDirectParameterWithGuardPredicate|objectRefWrapper
|current_application_constant
)
//handlerLabeledParametersCall //{pin=2}
//(handlerCallGuardPredicate|(tellObjectReference))
to (statement | handlerInterleavedParametersCall | expression ) 
//{pin=1}

handlerPositionalParametersDefinition ::= (on|to) identifier LPAREN [formalParameterList] RPAREN
                                            [varDeclarationList] nls
                                               blockBody? nls
                                            end [handlerNameStrict]
//                                            {pin=3} or Grammar kit will also make formalParameterList as Component
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}


left handlerPositionalParametersCall ::= LPAREN argumentList? RPAREN

formalParameterList ::= formalParameterListPart (COMMA formalParameterListPart)* //{recoverWhile="argument_list_recover"}
{ methods = [getFormalParameters] }

//todo впринципе можно тоже сделать это namedElement чтобы проще отображать в structureView
private formalParameterListPart ::= formalParameterListPartPattern {recoverWhile="argument_list_part_recover"}
//private formalParameterListPart ::= targetVariablePattern {recoverWhile="argument_list_part_recover"}
private argumentList ::= argumentListPart (COMMA argumentListPart)* {recoverWhile="argument_list_recover"}
private argument_list_recover ::= !(RPAREN)
private argumentListPart ::= expression {recoverWhile="argument_list_part_recover"}
private argument_list_part_recover ::= !(RPAREN|COMMA)

private formalParameterListPartPattern ::= simpleFormalParameter|listFormalParameter|recordFormalParameter
listFormalParameter ::= LCURLY [formalParameterListPartPattern|expression] (COMMA formalParameterListPartPattern|expression)* RCURLY //{pin=4}
{methods = [getParameters] }

recordFormalParameter ::= LCURLY[objectNamedPropertyDeclaration] (COMMA objectNamedPropertyDeclaration)* RCURLY
{methods = [getParameters] }

objectNamedPropertyDeclaration ::= propertyLabelIdentifier COLON (formalParameterListPartPattern|propertyInitializerExpression)
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

simpleFormalParameter ::= THE_KW? identifier
{mixin="com.idea.plugin.applescript.psi.impl.BaseAppleScriptComponent"
implements="com.idea.plugin.applescript.psi.AppleScriptComponent"}

//handlerRunDefinition ::= on run nls
//                            blockBody? nls
//                          end run {pin=2}
//
//handlerOpenDefinition ::= on open directParameterVar? nls
//                            blockBody? nls
//                           end open {pin=2}
//
//handlerIdleDefinition ::= on idle nls
//                            blockBody? nls
//                           end idle {pin=2}
//
//handlerQuitDefinition ::= on quit nls
//                            blockBody? nls
//                           end quit {pin=2}


handlerInterleavedParametersDefinition ::= (on|to) handlerInterleavedParametersSelectorPart+ nls
                                               blockBody? nls
                                             end [handlerNamePartRef COLON (handlerNamePartRef COLON)*]
{mixin="com.idea.plugin.applescript.psi.impl.AppleScriptHandlerInterleavedParameters"
implements="com.idea.plugin.applescript.psi.AppleScriptHandler"}

private handlerNamePartRef ::= referenceIdentifier

//todo change to a named element (but is named element - it is not selected by resolver...)
handlerInterleavedParametersSelectorPart ::= selectorId COLON userParameterVar
 //todo or remove component name from here at all, or other things to try (several componentNames etc)?
{mixin="com.idea.plugin.applescript.psi.impl.AppleScriptHandlerSelectorPartImpl"
implements="com.idea.plugin.applescript.psi.AppleScriptHandlerSelectorPart"}

//this is needed so to redefine getIdentifier for handlerInterleavedParametersSelectorPart
//not necessary should it be named element
selectorId ::= identifier
//{mixin="com.idea.plugin.applescript.psi.impl.AppleScriptNamedElementImpl"
//implements="com.idea.plugin.applescript.psi.AppleScriptNamedElement" }


handlerInterleavedParametersCall ::= handlerArgument+
{mixin="com.idea.plugin.applescript.psi.impl.AbstractAppleScriptHandlerCall"
implements="com.idea.plugin.applescript.psi.AppleScriptHandlerCall"}

handlerArgument ::= argumentSelector userParameterVal

scriptObjectDefinition ::= script scriptObjectName nls
                                  blockBody nls
                           end [script] //{pin=2}
{mixin="com.idea.plugin.applescript.psi.impl.AbstractAppleScriptScriptObject"
implements="com.idea.plugin.applescript.psi.AppleScriptScriptObject"}

scriptObjectUnnamedDefinition ::= script nls
                                  blockBody? nls
                                 end [script] {pin=5}

private scriptObjectName ::= identifier

// ??? continue handlerName [ parameterList ] ???
// ??? parameterList ??? on sayHello to someone | continue sayHello to someone
// структура - parameterList - лист параметров
// Any arguments following the script will be passed as a list of strings
// to the direct parameter of the ``run'' handler.  For example:
// http://hints.macworld.com/article.php?story=20050523140439734
continue_statement ::= continue expression



//===COMMANDS===
private apple_script_command_expression ::= getCommandExpression|runCommandExpression|countCommandExpression|errorCommandExpression
//|runCommandExpression|errorCommandExpression//|setCommandSyntaxReturning

// standard built-in AppleScript commands
//There are only five AppleScript commands: Get, Set, Count, Copy, and Run.
//>>copy<<
private copyCommandStatement ::= (copy|put) 
(((&(referenceIdentifier to)referenceIdentifier )|expression) (to|into) targetVariablePattern|expression) {pin=1}

//>>count<<
// AppleScript syntax
countCommandExpression ::= (count [[each|every] (typeSpecifierD|pluralClassName) (in|of)] composite_value)
                        |(number of [(typeSpecifierD|pluralClassName) (in|of)] composite_value)

//Application syntax:
//count [ each | every ] classType | pluralClassName [ ( in | of ) referenceToObject ]
//number of classType | pluralClassName [ ( in | of ) referenceToObject ]
//referenceToObject - A reference to the object or objects whose elements are to be counted. If you do not
// specify this parameter, the application counts the elements in the default target of the Tell statement.
//  Class: List, record, reference, or string

//The class name of the elements to be counted. The elements of lists, records, and strings are listed in
// the value class definitions in “Values and Constants” (page 51). The elements of application objects
// are listed in their object class definitions in the application dictionary. Class: Class identifier
//todo remove referenceIdentifier from here (it should be parsed lower)
private classTypeName ::= classTypeNameStrict|referenceIdentifier// add here all known classes names (file,string, etc)
private classTypeNameStrict ::= builtInClassIdentifier|script //|applicationClassName //script "name" - like argument
//todo is it neccesary. could be: text item, word item, document item etc...
private typeSpecifierD ::= dictionaryClassName|builtInClassIdentifier|builtInClassIdentifierPlural ITEM?
//|dictionaryPropertyName
//todo here could be declared user classType (like as for any other language MyClass "Name string")
//|userClassName

//private classTypeNameD ::= builtInClassName|builtInClassNamePlural|applicationClassName|script|<<parseDictionaryClassName>>
//todo here should be listed all parsed class names (dictionary objects, propertyLabels for the record?..)
//todo file 1 of application "Finder"
//|declaredObjectClass
//private declaredObjectClass ::= 
//private applicationClassName ::= file // folder-no - it does not understand Script Editor
private pluralClassName ::= builtInClassIdentifierPlural//|classType"s" //text plural=text !
//An expression that evaluates to a composite value whose elements are to be counted.
// Class: List, record, reference, or string
private composite_value ::= listLiteralExpression|recordLiteralExpression
                           |referenceExpression|stringLiteralExpression|referenceIdentifier
// >>>Result<< The result of the AppleScript command is an integer that specifies the number of elements of a specified
// class in a composite value.

//>>get<<
//The Get command can function as an AppleScript command or an application command. The AppleScript command
// returns the value of an expression. The application command returns the value of an object.
// AppleScript syntax
//private cmd_get_expression ::= [get] expression [as class_name]
getCommandExpression ::= get expression
                      //|([get] objectReferenceWrapper [coercion_expression])//Application syntax ()
                              //when application (ref -> gmd_get->obj_ref_>...)object_ref turns into get command

//>>Run<< / Run Script
//The AppleScript Run command acts on script objects. It causes statements in a script object definition,
// other than handler and property definitions, to be executed.
runCommandExpression ::= (run [script] [script_object_variable])|(run [referenceToApplicationVar]) // application command syntax
private referenceToApplicationVar ::= application_reference
private script_object_variable ::= referenceIdentifier
//scriptObjectVariable A variable identifier whose value is a script object. This parameter is optional if the
// Run command is used within an appropriate Tell statement. Class: Script

//>>Set<<
//The AppleScript command assigns one or more values to one or more variables. It can also be used to share
// data among lists, records, or script objects—see the Notes section below and “Data Sharing” (page 206).
//private cmd_set_statement ::= setCommandSyntaxSet|setCommandSyntaxReturning

//todo and com.jetbrains.python.psi.impl.PyTargetExpressionImpl#findAssignedValue
//todo to check com.jetbrains.python.psi.PyAssignmentStatement#getTargetsToValuesMapping
//todo see com.jetbrains.python.psi.impl.PyAssignmentStatementImpl#mapToValues
//todo ???may be creat new class for targetVariable ? which will extend component and implement namedElement in one class???
//todo then hierarchy could be broken...?
private parseAssignmentStatement ::= <<parseAssignmentStatementInner>>
assignmentStatement ::= setCommandAppleScript|setCommandApplication|copyCommandStatement
{methods=[getAssignmentTarget getTargets getTargetsToValuesMapping]}

//set AppleScript command
private setCommandAppleScript ::= setCommandAppleScriptSetSyntax|setCommandAppleScriptReturningSyntax
//todo why it is not recovered without & here?? (targetVariable->targetVariable ... ?
private setCommandAppleScriptSetSyntax ::= set THE_KW?
(( (&(targetVariablePattern to)targetVariablePattern) | objectRefWrapper ) to THE_KW? expression/*|expression*/) //{pin=1}//fall back to single expression=(
private setCommandAppleScriptReturningSyntax ::= expression returning targetVariablePattern {pin=2}
//set Application command
private setCommandApplication ::= setCommandApplicationSetSyntax|setCommandApplicationReturningSyntax
private setCommandApplicationSetSyntax ::= set referencePatternD to expression {pin=1}
private setCommandApplicationReturningSyntax ::=  expression returning referencePatternD {pin=2}
//private setCommandApplicationSetSyntax ::= set expression to expression {pin=1}

//The name of the variable in which to store the value, or a list of variable patterns,
//or a record of variable patterns. Class: Identifier, list, or record
private targetVariablePattern ::= targetVariable|targetListLiteral|targetRecordLiteral

private referencePattern ::= referenceExpression //variable reference /list of variable refs/record of refs
//private referencePatternD ::= referenceExpressionD objectRef? //referenceExpressionD //variable reference /list of variable refs/record of refs
private referencePatternD ::= referenceExpressionD ((of|in|APS) THE_KW? referenceExpressionD)?// objectRef? //referenceExpressionD //variable reference /list of variable refs/record of refs


// OPERATORS & EXPRESSIONS
// AppleScript includes operators for performing arithmetic operations(numeric_ex), comparing values(testing_ex),
// performing Boolean evaluations(logical ex), and coercing values.
// todo operator precedence (coercion and not order amend) also & insert here
private logicalOrExpressionWrapper ::= logicalAndExpressionWrapper logicalOrExpression*
left logicalOrExpression ::= LOR nls logicalAndExpressionWrapper

private logicalAndExpressionWrapper ::= compareExpressionWrapper logicalAndExpression*
left logicalAndExpression ::= LAND nls compareExpressionWrapper

private compareExpressionWrapper ::= concatenationExpressionWrapper compareExpression*
left compareExpression ::= (equalityOperator|relational_operator) nls concatenationExpressionWrapper

private concatenationExpressionWrapper ::= additiveExpressionWrapper concatenationExpression*
left concatenationExpression ::= BAND additiveExpressionWrapper

private additiveExpressionWrapper ::= multiplicativeExpressionWrapper additiveExpression*
left additiveExpression ::= (PLUS|MINUS) nls multiplicativeExpressionWrapper

private multiplicativeExpressionWrapper ::= powerExpressionWrapper multiplicativeExpression*
left multiplicativeExpression ::= multiplicativeOperator nls powerExpressionWrapper

private powerExpressionWrapper ::= objectRefWrapper powerExpression*
left powerExpression ::= POW objectRefWrapper


private prefixExpression ::= (prefixOperator nls prefixExpression) | valueExpression
private prefixOperator ::= PLUS|MINUS|LNOT

//private objectRefWrapper ::=
//THE_KW? ((prefixExpression APS THE_KW? handlerInterleavedCallOrPrefixExpression)|prefixExpression) [filterReference]
//((THE_KW? applicationObjectReference)|objectRef*)
private objectRefWrapper ::= THE_KW? prefixExpression filterReference?
 ( (THE_KW? applicationObjectReference) | objectRef* )

private possessiveFormAndInterleavedCallOrPrimaryWithNoHandlerCall ::=
APS (handlerInterleavedParametersCall | primaryExpressionNoLabeledHandlerCall)
//private handlerInterleavedCallOrPrefixExpression ::= handlerInterleavedParametersCall | prefixExpression

left objectRef ::=
//todo: to workaround problem with such calls:
//todo: tell current application's CtrlrSubclassAppDelegate to changeButtonImage() (name_1's name_2 to Expr)
( ( possessiveFormAndInterleavedCallOrPrimaryWithNoHandlerCall | (of|in|APS) THE_KW? prefixExpression ) filterReference? THE_KW? applicationObjectReference?)
//todo other solution (and better performance) to fallback to: tell expr handlerLabeledCall
//( (of|in|APS) THE_KW? prefixExpression filterReference? THE_KW? applicationObjectReference?)

private valueExpression ::= primaryExpressionD coercionExpression?

commandHandlerCallExpression ::= <<parseCommandHandlerCallExpression>>

//commandHandlerCallExpression ::= dictionaryCommandName dictionaryCommandParameters?
external dictionaryCommandParameters ::= parseParametersForCommand parsingCommand

//tryCommandHandlerCallExpression ::= dictionaryCommandName dictionaryCommandParameters?
//commandHandlerCallExpression ::= <<parseDictionaryCommandNameInner parsedCommandName>>
//<<parseParametersForCommand parsedCommandName>>
//parseNodeCommandHandlerCallExpression ::= <<parseCommandHandlerCallExpression>>
//todo should consume all words which are not keywords
//todo do not change the name: used for DICTIONARY_COMMAND_NAME IElementType !!!
// used in parseDictionaryCommandNameInnerInner(com.intellij.lang.PsiBuilder, int, org.omg.CORBA.StringHolder)
//external dictionaryCommandName ::= <<parseDictionaryCommandNameInner new StringHolder()>>
//external dictionaryCommandNameExternalRule ::= parseDictionaryCommandNameInner parsedName
//commandParameterSelector ::= <<parseCommandParameterSelector>>
dictionaryCommandName ::= <<parseDictionaryCommandNameInner>>
//private dictionaryCommandName ::= <<parseDictionaryCommandNameInner parsedCommandName>> //dictionaryCommandNameExternalRule

private dictionaryCommandNameTokens ::= identifier|builtInClassIdentifier|builtInClassIdentifierPlural|handlerParameterLabel
|run|count|script//|with|without|given set/copy ??

//private tryCommandHandlerCallExpression ::= <<parseDictionaryCommandName>>

//private tryCommandHandlerCallExpression ::= <<parseCommandHandlerCallExpression>>
//todo do shell script "name -params" & quoted form of e without altering line endings 
//quoted form of e without altering line endings is a labeledHandler call !! check parsing order
private primaryExpressionD ::=
     literalExpression
 |   commandHandlerCallExpression
 | ( referenceExpressionD (   ( <<isParsingCommandHandlerCallParameters>> handlerPositionalParametersCall? )
                            | ( <<isTreePrevSimpleReference>> ( handlerLabeledParametersCall | handlerPositionalParametersCall ) )?
                          )
   )
 |   parenthesizedExpression
 |   apple_script_command_expression

private referenceExpressionD ::=
    refFormD
  | application_reference
  | file_reference
  | (referenceIdentifier /*(subRoutineHandlerCall|handlerPositionalParametersCall)?*/)

//todo quoted form of e without altering line endings is a labeledHandler call !! check parsing order
private refFormD ::= 
      (my handlerInterleavedCallOrPropertyReferenceDOrReferenceIdentifier ) //itMeProperty IN LITERAL expressions ... D?
    | (my?
       ( arbitraryReferenceD
    |    everyElemReferenceD
    |    rangeReferenceD
    |    nameReferenceD
    |    middleElemReferenceD
    |    relativeReferenceD
    |    indexReferenceD
    |    propertyReferenceD //handlerCallExpression?
        )
       )



//todo need to get rig of this somehow
primaryExpressionNoLabeledHandlerCall ::=
                             literalExpression
                             |referenceExpressionD handlerPositionalParametersCall? // only labeled params handler is left out..?
                             |parenthesizedExpression
                             |apple_script_command_expression

//private handlerCallExpression ::= handlerLabeledParametersCall|handlerPositionalParametersCall
//|handlerInterleavedParametersCall


//todo to consider remove literal expression from reference_expression ?
private referenceExpression ::=  // page 190
                                //|literal_object_reference //included in propertyRef (rational?)
                                (refForm
                                |application_reference
                                //|file_reference
                                |referenceIdentifier) //handlerCallExpression?//how to include here and not in property ref?

private equalityOperator ::= NE|EQ
private relational_operator ::= LT|GT|LE|GE|containment_start_end_operator|containment_any_part_operator
                        //|as class_type
//otherwise it is hard to write rules separately for containment or relational
// todo need to control it(argument types) with an inspections
//private containment_expression ::= containment_any_part_expression|containment_start_end_expression

// List and String
private containment_start_end_operator ::= STARTS_BEGINS_WITH|ENDS_WITH
//List, Record, String
private containment_any_part_operator ::= IS_CONTAIN|DOES_NOT_CONTAIN|IS_IN|IS_NOT_IN

private multiplicativeOperator ::= STAR|DIV|INT_DIV|MOD

left coercionExpression ::= coercionBuiltInExpression|coercionExternalExpression
private coercionBuiltInExpression ::= AS nls typeSpecifierD {pin=3}
private coercionExternalExpression ::= AS nls referenceIdentifier {pin=3}
